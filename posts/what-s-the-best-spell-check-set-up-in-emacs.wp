
<p>To save your time, I started from conclusion.
</p>

<div id="outline-container-1" class="outline-3">
<h3 id="sec-1">Suggestion for non-programmers</h3>
<div class="outline-text-3" id="text-1">

<p>Emacs will find the right dictionary by querying your locale.
</p>
<p>
Run command "locale" in your shell to get current locale.
</p>
<p>
If you want to force Emacs use dictionary "en_US", copy below code into your ~/.emacs:
</p>



<pre class="prettyprint lang-lisp">
;; find aspell and hunspell automatically
(cond
 ((executable-find "aspell")
  (setq ispell-program-name "aspell")
  (setq ispell-extra-args '("--sug-mode=ultra" "--lang=en_US")))
 ((executable-find "hunspell")
  (setq ispell-program-name "hunspell")
  (setq ispell-extra-args '("-d en_US")))
 )
</pre>



<p>
That's it!
</p>
<p>
Please run command "man aspell" or "man hunspell" in shell if you have more questions. I've nothing more to say.
</p>
</div>

</div>

<div id="outline-container-2" class="outline-3">
<h3 id="sec-2">Suggestion for programmers</h3>
<div class="outline-text-3" id="text-2">

<p>I strongly recommend aspell instead of hunspell (Though hunspell is fine).
</p>
<p>
Please insert below code into your ~/.emacs:
</p>




<pre class="prettyprint lang-lisp">
;; if (aspell installed) { use aspell}
;; else if (hunspell installed) { use hunspell }
;; whatever spell checker I use, I always use English dictionary
;; I prefer use aspell because:
;; 1. aspell is older
;; 2. looks Kevin Atkinson still get some road map for aspell:
;; @see http://lists.gnu.org/archive/html/aspell-announce/2011-09/msg00000.html
(defun flyspell-detect-ispell-args (&amp;optional RUN-TOGETHER)
  "if RUN-TOGETHER is true, spell check the CamelCase words"
  (let (args)
    (cond
     ((string-match  "aspell$" ispell-program-name)
      ;; force the English dictionary, support Camel Case spelling check (tested with aspell 0.6)
      (setq args (list "--sug-mode=ultra" "--lang=en_US"))
      (if RUN-TOGETHER
          (setq args (append args '("--run-together" "--run-together-limit=5" "--run-together-min=2")))))
     ((string-match "hunspell$" ispell-program-name)
      (setq args nil)))
    args
    ))

(cond
 ((executable-find "aspell")
  (setq ispell-program-name "aspell"))
 ((executable-find "hunspell")
  (setq ispell-program-name "hunspell")
  ;; just reset dictionary to the safe one "en_US" for hunspell.
  ;; if we need use different dictionary, we specify it in command line arguments
  (setq ispell-local-dictionary "en_US")
  (setq ispell-local-dictionary-alist
        '(("en_US" "[[:alpha:]]" "[^[:alpha:]]" "[']" nil nil nil utf-8))))
 (t (setq ispell-program-name nil)))

;; ispell-cmd-args is useless, it's the list of *extra* arguments we will append to the ispell process when "ispell-word" is called.
;; ispell-extra-args is the command arguments which will *always* be used when start ispell process
(setq ispell-extra-args (flyspell-detect-ispell-args t))
;; (setq ispell-cmd-args (flyspell-detect-ispell-args))
(defadvice ispell-word (around my-ispell-word activate)
  (let ((old-ispell-extra-args ispell-extra-args))
    (ispell-kill-ispell t)
    (setq ispell-extra-args (flyspell-detect-ispell-args))
    ad-do-it
    (setq ispell-extra-args old-ispell-extra-args)
    (ispell-kill-ispell t)
    ))
</pre>



<p>
That's it.
</p>
</div>

</div>

<div id="outline-container-3" class="outline-3">
<h3 id="sec-3">Why</h3>
<div class="outline-text-3" id="text-3">

<ul>
<li id="sec-3-1">Aspell<br/>
apell is recommended because its option "&ndash;run-together". That option could spell check the camel case word. Variable name often uses camel case naming convention these days. Read my <a href="http://blog.binchen.org/?p=950">Effective spell check in Emacs</a> for advanced tips on how to use flyspell to check variable names.

<p>
If Emacs start a aspell process with "&ndash;run-together" option, that process is not closed so it can be re-used by other commands.
</p>
<p>
This behaviour will be a problem if you want to let Emacs/aspell correct the typo by running the command "ispell-word" because a apell process with "&ndash;run-together" will produce much noise.
</p>
<p>
For example, for a typo "helle" Emacs will give you too many candidates. It's hard to find the desired word "hello":
<img src="/wp-content/aspell-camelcase-suggest-nq8.png"  alt="aspell-camelcase-suggest-nq8.png" />
</p>
<p>
The better solution is before running "M-x ispell-word", we'd better restart a aspell proces without the argument "&ndash;run-together".
</p>
<p>
Here is the screen shot after we applying this fix:
<img src="/wp-content/aspell-normal-suggest-nq8.png"  alt="aspell-normal-suggest-nq8.png" />
</p>
<p>
As I mentioned, the global variable "ispell-extra-args" contains arguments Emacs will always append to a spell checker process (aspell or hunspell). That's the only variable you need care about.
</p>
<p>
There is another variable named "ispell-cmd-args". It is actually some <b>extra</b> arguments Emacs could send to an <b>existing</b> spell checker process when you "M-x ispell-word". In my opinion, it's useless. I mention it because the naming are really confusing. "ispell-extra-args" is actually command line arguments the spell checker will always use. The "ispell-cmd-args" are actually the extra arguments will be used in certain cases.
</p></li>
</ul>
<ul>
<li id="sec-3-2">Hunspell<br/>
I cannot find hunspell option to check camel case words. Please enlighten me if you know the option.

<p>
Hunspell has some design flaw. It will always check the environment variable LC_ALL, LC_MESSAGES and LANG <b>at first</b> to find the default dictionary unless you specify the dictionary in the command line. If it cannot find the default dictionary, the spell checker process won't start. Aspell does not have this issue, if it cannot find the zh_CN dictionary, it will fall back into English.
</p>
<p>
Specify the ispell-extra-args <b>won't stop hunspell to search for the default dictionary</b> at the beginning.
</p>
<p>
For example, I am a Chinese and my locale is "zh_CN.utf-8". So hunspell will always search the dictionary zh_CN. Even I'm only interested in English spell checking.
</p>
<p>
To specify the dictionary explicitly, I need hack the Emacs code which is kind of mess. Finally, I figure out. It's just several lines of elisp code:
</p>




<pre class="prettyprint lang-lisp">
(setq ispell-program-name "hunspell")
(setq ispell-local-dictionary "en_US")
(setq ispell-local-dictionary-alist
      '(("en_US" "[[:alpha:]]" "[^[:alpha:]]" "[']" nil nil nil utf-8)))
</pre>



</li>
</ul>
</div>
</div>
